import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask

apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext.versions = [
            'libVersionCode'  : 31003,
            'libVersionName'  : '3.1.3',
            'compileSdk'      : 30,
            'minSdk'          : 14,
            'targetSdk'       : 30,
            'buildTools'      : '30.0.3',
            'okhttp'          : '3.12.0', // LTS version, update until December 31, 2021.
            'coroutines'      : '1.4.2',
            'testCoroutines'  : '1.4.2',
            'kotlin'          : '1.4.21',
            'appCompat'       : '1.2.0',
            'constraintLayout': '2.0.4',
            'material'        : '1.2.1',
            'androidx'        : '1.0.0',
            'lifecycle'       : '2.2.0',
            'picasso'         : '2.71828',
            'junit'           : '4.13.1',
            'extJunit'        : '1.1.0',
            'espresso'        : '3.1.1',
            'mockito'         : '2.23.0',
            'room'            : '2.2.6',
            'swipe'           : '1.1.0',
            'activityAndroidx': '1.1.0',
            'androidXTest'    : '1.3.0',
            'mockitoKotlin'   : '2.2.0',
            'mockitoInline'   : '3.7.7',
            'mockitoCore'     : '3.7.7'
    ]

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.36.0"
    }
}

allprojects {
    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def isNonStable = { String version ->
    def regex = /^[0-9,.v-]+(-r)?$/
    return !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    // Example 1: reject all non stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}